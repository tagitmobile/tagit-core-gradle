println "[tagit-rest] Applying TagitCore v7.5 Boot WAR plug-ins, configurations and dependencies for Spring Boot applications"

apply plugin: "war"
apply plugin: 'org.springframework.boot'
//apply plugin: 'io.spring.dependency-management' // [rj.commented 2021.10.07] Use Gradle's BOM support instead from tagit-core-rest

ext {
	// default a tomcat version
	if (!project.hasProperty("tomcatVersion")) {
	    tomcatVersion = "10.1.17"
	}
	println "[tagit-rest] Overriding Tomcat Version to ${tomcatVersion}"
}

configurations.all {
	exclude module: 'spring-boot-starter-logging' // to replace with log4j2 and fix the exception Exception in thread "main" java.lang.StackOverflowError at org.apache.logging.log4j.util.StackLocator.getCallerClass(StackLocator.java:112)

	resolutionStrategy.force "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}"
	resolutionStrategy.force "org.apache.tomcat.embed:tomcat-embed-el:${tomcatVersion}"
	resolutionStrategy.force "org.apache.tomcat.embed:tomcat-embed-websocket:${tomcatVersion}"
}

def getDate() {
    return new Date().format('yyyyMMddHHmmss')
}

// By default, when the bootJar or bootWar tasks are configured, the jar or war tasks are disabled. A project can be configured to build both an executable archive and a normal archive at the same time by enabling the jar or war task:
jar {
	enabled = true
	archiveClassifier = ''
}
war {
	enabled = false
}

// To avoid the executable archive and the normal archive from being written to the same location, one or the other should be configured to use a different location. One way to do so is by configuring a classifier:
bootJar {
	enabled = true
	archiveClassifier = 'boot'
	
	// [rj.commented 2021.10.18] Conflicts with bootBuildImage: not compatible with buildpacks; ensure jar file is valid and launch script is not enabled
// 	launchScript() // Spring Boot provides support for fully executable archives. An archive is made fully executable by prepending a shell script that knows how to launch the application. On Unix-like platforms, this launch script allows the archive to be run directly like any other executable or to be installed as a service.
	
	from('src/main/webapp') {
        into '/public'
    }

    manifest {
		attributes("Specification-Title": "${projectName}")
		attributes("Specification-Version": archiveVersion) // [rj.updated 2020.06.01] gradle 6.4.1 support instead of version
		attributes("Specification-Vendor": "Tagit Pte Ltd")        
		attributes("Build-Date": getDate())		
    }    	
}

bootWar {
	enabled = true
	includes = ["WEB-INF/**", "META-INF/**"]
    manifest {
		attributes("Specification-Title": "${projectName}")
		attributes("Specification-Version": archiveVersion) // [rj.updated 2020.06.01] gradle 6.4.1 support instead of version
		attributes("Specification-Vendor": "Tagit Pte Ltd")        
		attributes("Build-Date": getDate())		
    }
}

// [rj.added 2023.10.31] check if the publish task exists
if (project.tasks.findByName('publish')) {
	publishing {
		publications {
			// [rj.commented.2023.11.20] this is publishing to a different snapshot entry from mavenJava
			// bootJava(MavenPublication) {
			// 	artifact tasks.named("bootWar")
			// }
			mavenJava(MavenPublication) {
//				from components.web // Maven publication 'mavenJava' cannot include multiple components
				artifact tasks.named("bootWar")
			}
	    }
	}
	println "[tagit-rest] Detected publish task. Registering Boot WAR artifacts to be published."
}

task copyWar(type: Copy) {
    dependsOn bootWar
    from war, bootJar, bootWar
    into "deploy"
}
copyJar.dependsOn copyWar

// [rj.added 2020.06.01] common dependencies for TagitCore Rest applications
dependencies {

	// [rj.added 2023.06.13] align with TagitCore BOM as a standard
	implementation platform("com.tagit.commons:tagit-core-bom:${tagitCoreVersion}")
	providedRuntime platform("com.tagit.commons:tagit-core-bom:${tagitCoreVersion}")

    // ### Unit Testing ###
	testImplementation("com.tagit.commons:tagit-core-test")
	testImplementation("com.tagit.commons:tagit-core-test") {
		capabilities {
	    	requireCapability("com.tagit.commons:tagit-core-test-boot")
        }
	}
	testRuntimeOnly("com.tagit.commons:tagit-core-test") {
		capabilities {
	    	requireCapability("com.tagit.commons:tagit-core-test-runtime")
        }
	}
	
	// ### Runtime Standalone ###
//	compileOnly "javax.servlet:javax.servlet-api" // instead of tomcat, this allows seamless deployment to J2EE containers
	// https://mvnrepository.com/artifact/jakarta.servlet/jakarta.servlet-api
	compileOnly "jakarta.servlet:jakarta.servlet-api" // [rj.added 2023.05.03] Spring Boot 3 migrated to Jakarta EE 10

	providedRuntime "org.springframework.boot:spring-boot-starter-tomcat"

	// --------------------------------------------------
	// DB CONNECTIVITY
	// --------------------------------------------------
	providedRuntime("com.tagit.commons:tagit-core-db")
	
}
defaultTasks 'clean', 'eclipse', 'build', 'bootJar', 'copyWar', 'htmlDependencyReport', 'copyDependencies'
